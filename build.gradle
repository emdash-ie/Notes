import java.nio.file.Path
import java.io.File
import java.util.HashSet
import java.util.Set

buildDir '/Users/Noel/Desktop/Temp'

task buildNotes(type : PandocTask) {
    group 'Build'
    description 'Builds the notes using pandoc'

    inputFiles = fileTree(dir: '.', include: '**/*.md')

    outputCalculator = { File input ->
        Path relativePath = projectDir.toPath().relativize(input.toPath())
        Path outputPath = buildDir.toPath().resolve(relativePath)
        String outputString = outputPath.toString().replace(".md", ".pdf")
        return new File(outputString)
    }
}

class PandocTask extends DefaultTask {
    Closure<File> outputCalculator
    FileCollection inputFiles
    private Iterable<File> outputFiles

    @InputFiles
    public FileCollection getInputFiles() {
        return inputFiles
    }

    @OutputFiles
    public Iterable<File> getOutputFiles() {
        if (outputFiles == null) {
            outputFiles = determineOutputFiles()
        }
        return outputFiles
    }

    private Iterable<File> determineOutputFiles() {
        Set<File> outputs = new HashSet<File>()
        inputFiles.each { File input ->
            outputs.add(outputCalculator(input))
        }
        return outputs
    }

    @TaskAction
    public void runPandoc() {
        inputFiles.each {File file ->
            project.exec {
                executable '/usr/local/bin/pandoc'
                args = [
                    "${file.getAbsolutePath()}",
                    "-o", "${outputCalculator(file).getAbsolutePath()}",
                    "-s",
                    "--highlight-style", "tango",
                    "--pdf-engine=xelatex",
                    "-V", "margin-top=1in",
                    "-V", "margin-bottom=1in",
                    "-V", "margin-left=1.25in",
                    "-V", "margin-right=1.25in",
                    "-V", "mainfont=Source Serif Pro",
                    "-V", "sansfont=Source Sans Pro",
                    "-V", "monofont=Source Code Pro",
                    "-V", "fontsize=12pt"
                ]
            }
        }
    }

}
